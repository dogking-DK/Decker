#version 460

layout (local_size_x = 16, local_size_y = 16) in;

// 绑定：binding 0 = 采样源，binding 1 = 可写目标
layout (binding = 0) uniform sampler2D uSrc;
layout (binding = 1, rgba16f) writeonly uniform image2D uDst;

layout (push_constant) uniform PushConstants {
    vec2 direction; // (1,0) 横向, (0,1) 纵向
    vec2 texelSize; // 1/width, 1/height
} pc;

// 这里用半径 = 5 的核，权重可以预先算好直接写死
const int RADIUS = 5;
const float kernel[11] = float[](
    0.009167, 0.01453, 0.020595, 0.026995, 0.032418,
    0.035855,
    0.032418, 0.026995, 0.020595, 0.01453, 0.009167
);

void main()
{
    ivec2 gid = ivec2(gl_GlobalInvocationID.xy);
    ivec2 size = imageSize(uDst);
    if (gid.x >= size.x || gid.y >= size.y) {
        return;
    }

    vec2 uv = (vec2(gid) + 0.5) * pc.texelSize;

    vec3 color = vec3(0.0);
    float sumW = 0.0;

    for (int i = -RADIUS; i <= RADIUS; ++i) {
        float w = kernel[i + RADIUS];
        vec2 offset = float(i) * pc.direction * pc.texelSize;
        vec2 sampleUv = uv + offset;
        color += texture(uSrc, sampleUv).rgb * w;
        sumW += w;
    }

    color /= sumW;

    imageStore(uDst, gid, vec4(color, 1.0));
}
