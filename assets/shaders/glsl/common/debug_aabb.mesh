#version 460
#extension GL_EXT_mesh_shader : require

layout(local_size_x = 64, local_size_y = 1, local_size_z = 1) in;

layout(lines) out;
layout(max_vertices = 128, max_primitives = 64) out;

layout(set = 0, binding = 0) readonly buffer LineBuffer {
    vec4 positions[];
} lineBuffer;

layout(push_constant) uniform PushConstants
{
    mat4 viewproj;
    vec4 color;
} pc;

layout(location = 0) out PerVertexData {
    vec4 color;
} outData[];

void main()
{
    uint lineIndex = gl_GlobalInvocationID.x;
    uint lineCount = lineBuffer.positions.length() / 2;
    if (lineIndex >= lineCount)
    {
        return;
    }

    uint localIndex = gl_LocalInvocationIndex;
    uint baseVertex = localIndex * 2;
    uint baseBufferIndex = lineIndex * 2;

    vec4 p0 = lineBuffer.positions[baseBufferIndex + 0];
    vec4 p1 = lineBuffer.positions[baseBufferIndex + 1];

    gl_MeshVerticesEXT[baseVertex + 0].gl_Position = pc.viewproj * p0;
    gl_MeshVerticesEXT[baseVertex + 1].gl_Position = pc.viewproj * p1;
    outData[baseVertex + 0].color = pc.color;
    outData[baseVertex + 1].color = pc.color;

    gl_PrimitiveLineIndicesEXT[localIndex] = uvec2(baseVertex + 0, baseVertex + 1);

    if (localIndex == 0)
    {
        uint remaining = lineCount - gl_WorkGroupID.x * gl_WorkGroupSize.x;
        uint linesInGroup = min(gl_WorkGroupSize.x, remaining);
        SetMeshOutputsEXT(linesInGroup * 2, linesInGroup);
    }
}
